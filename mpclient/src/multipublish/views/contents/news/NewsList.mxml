<?xml version="1.0" encoding="utf-8"?>
<wt:Vessel xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx"
		   xmlns:wt="com.winonetech.core.*"
		   xmlns:cp="com.winonetech.components.*"
		   mouseEnabled="false">
	
	<wt:states>
		<s:State name="single"/>
		<s:State name="multi"/>
	</wt:states>
	
	<s:Group width="100%" height="100%" mouseEnabled="false">
		
		<s:layout>
			<s:VerticalLayout id="newsLayout" gap="10"
							  horizontalAlign="center"
							  paddingLeft="10" paddingRight="10"
							  paddingTop="10" paddingBottom="10"/>
		</s:layout>
		
		<s:VGroup id="container" width="100%" height="100%"
				  gap="10" mouseEnabled="false"/>
		
		<cp:PageSlider id="slider" includeIn="multi" 
					   skinClass="multipublish.skins.PageSliderSkin"
					   change="slider_changeHandler(event)"/>
		
	</s:Group>
	
	<fx:Script>
		<![CDATA[
			
			import cn.vision.utils.MathUtil;
			
			import multipublish.core.mp;
			
			
			/**
			 * 
			 * 切换至第几页。
			 * 
			 */
			
			public function gotoPage($page:int = 0, $update:Boolean = false):void
			{
				if (itemTotal && pageTotal >= 1 && (pageCurrent != $page || $update))
				{
					pageCurrent = MathUtil.clamp($page, 0, Math.max(0, pageTotal - 1));
					
					container.removeAllElements();
					
					var start:uint = pageCurrent * tempCount;
					var end:uint = Math.min((pageCurrent + 1) * tempCount, itemTotal);
					var i:uint, item:NewsItem;
					
					for (i = start; i <end; i++)
					{
						item = new NewsItem;
						item.data = data[i];;
						container.addElement(item);
					}
				}
			}
			
			
			/**
			 * @private
			 */
			private function caculateCount(length:uint):void
			{
				itemTotal = length;
				var result:Number = caculateRow((height - newsLayout.paddingTop - newsLayout.paddingBottom), itemHeight, container.gap);
				pageCount = result;
				
				currentState = itemTotal > pageCount ? "multi" : "single";
				
				if (currentState == "single")
				{
					tempCount = pageCount;
					pageTotal = 1;
				}
				else
				{
					result = caculateRow(height - newsLayout.paddingTop - newsLayout.paddingBottom - 55, itemHeight, container.gap);
					tempCount = result;
					
					pageTotal = (itemTotal % tempCount == 0) 
						? itemTotal / tempCount 
						: itemTotal / tempCount + 1;
					
					if (slider)
					{
						slider.max = pageTotal;
						slider.min = slider.current = 1;
					}
				}
			}
			
			/**
			 * @private
			 */
			private function caculateRow($height:Number, $item:Number, $gap:Number):Number
			{
				return ($height + $gap) / ($item + $gap);
			}
			
			
			/**
			 * @private
			 */
			private function slider_changeHandler($e:Event):void
			{
				gotoPage(slider.current - 1);
			}
			
			/**
			 * 
			 * 数据源。
			 * 
			 */
			
			public function get data():Object
			{
				return mp::data;
			}
			
			/**
			 * @private
			 */
			public function set data($value:Object):void
			{
				mp::data = $value;
				
				applyCallback(callbackData);
			}
			
			/**
			 * @private
			 */
			private function callbackData():Boolean
			{
				var result:Boolean = newsLayout && container && height;
				if (result)
				{
					caculateCount(data ? data.length : 0);
					
					gotoPage(0, true);
				}
				return result;
			}
			
			
			/**
			 * @private
			 */
			private const itemHeight:Number = 150;
			
			
			/**
			 * @private
			 */
			private var pageCount:uint;
			
			/**
			 * @private
			 */
			private var tempCount:uint;
			
			/**
			 * @private
			 */
			private var pageCurrent:uint;
			
			/**
			 * @private
			 */
			private var pageTotal:uint;
			
			/**
			 * @private
			 */
			private var itemTotal:uint;
			
			
			/**
			 * @private
			 */
			mp var data:Object;
			
		]]>
	</fx:Script>
	
</wt:Vessel>
