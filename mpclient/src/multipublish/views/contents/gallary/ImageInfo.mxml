<?xml version="1.0" encoding="utf-8"?>
<wt:Vessel xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s ="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx"
		   xmlns:wt="com.winonetech.core.*"
		   xmlns:ct="com.winonetech.controls.*">
	
	<wt:states>
		<s:State name="media"/>
		<s:State name="image"/>
	</wt:states>
	
	<s:Rect width="100%" height="100%">
		<s:stroke>
			<s:SolidColorStroke color="0xCCCCCC"/>
		</s:stroke>
	</s:Rect>
	
	<s:Group width="100%" height="100%" id="contentContainer">
		
		<s:layout>
			<s:VerticalLayout gap="10" horizontalAlign="center"
							  paddingLeft="20" paddingRight="20"
							  paddingBottom="20" paddingTop="20"/>
		</s:layout>
		
		<s:Label fontSize="50" includeIn="media" text="{data.title}"
				 width="100%" maxDisplayedLines="2" textAlign="center"/>
		
		<s:Label fontSize="30" color="0x666666" includeIn="media" text="{data.subTitle}"
				 id="subTitleLabel" width="100%" maxDisplayedLines="2" textAlign="center"/>
		
		<mx:HRule width="98%" includeIn="media"/>
		
		<s:Group id="imagesContainer" width="100%" height="100%"/>
		
	</s:Group>
	
	<s:Group width="100%" bottom="0" clipAndEnableScrolling="true" includeIn="media">
		
		<s:BorderContainer id="textContainer" left="1" right="2"
						   borderVisible="false" height="60"
						   backgroundColor="0" backgroundAlpha=".5">
			
			<mx:Text id="text" fontSize="16" selectable="false"
					 color="0xFFFFFF" left="0" right="60" 
					 paddingLeft="5" paddingRight="5"
					 paddingTop="5" paddingBottom="5"
					 text="{data.content}"/>
			
			<s:ToggleButton id="expandBtn" width="50" height="50"
							skinClass="multipublish.skins.ExpandToggleButtonSkin"
							right="5" top="5" label="↑"
							click="button_clickHandler(event)"/>
			
		</s:BorderContainer>
		
	</s:Group>
	
	<fx:Script>
		<![CDATA[
			
			import cn.vision.utils.FileUtil;
			import cn.vision.utils.ObjectUtil;
			import cn.vision.utils.StringUtil;
			
			import multipublish.core.mp;
			
			import mx.binding.utils.BindingUtils;
			import mx.events.ResizeEvent;
			import mx.graphics.BitmapFillMode;
			import mx.graphics.BitmapScaleMode;
			
			import spark.components.Image;
			import spark.components.VideoPlayer;
			
			
			
			/**
			 * 
			 * 显示数据。
			 * 
			 */
			
			public function show($data:Object):void
			{
				stop();
				
				data = $data;
			}
			
			
			/**
			 * 
			 * 显示数据。
			 * 
			 */
			
			public function stop():void
			{
				if (video) video.stop();
			}
			
			
			/**
			 * 
			 * 隐藏。
			 * 
			 */
			
			public function reset():void
			{
				if (imagesContainer)
					imagesContainer.removeAllElements();
				
				if (video)
				{
					video.stop();
					video.source = null;
					video = null;
				}
				
				if (image)
				{
					image.source = null;
					image = null;
				}
				
			}
			
			
			/**
			 * @private
			 */
			private function resolveMedia():void
			{
				imagesContainer.removeAllElements();
				if(!contentContainer.containsElement(imagesContainer))
					contentContainer.addElement(imagesContainer);
				
				if (data.source)
				{
					var ext:String = FileUtil.getFileTypeByURL(data.source);
					
					switch (ext)
					{
						case "png":
						case "jpg":
						case "jpeg":
							image = new Image;
							image.fillMode  = BitmapFillMode.SCALE;
							image.scaleMode = BitmapScaleMode.LETTERBOX;
							image.percentHeight = image.percentWidth = 100;
							image.source = data.source;
							imagesContainer.addElement(image);
							break;
						case "mp4":
						case "flv":
							video = new VideoPlayer;
							video.setStyle("skinClass", multipublish.skins.VideoPlayerSkin);
							video.autoPlay = false;
							video.autoDisplayFirstFrame = true;
							video.autoRewind = true;
							video.percentHeight = video.percentWidth = 100;
							
							video.source = data.source;
							imagesContainer.addElement(video);
							break;
					}
				}
			}
			
			
			/**
			 * @private
			 */
			private function button_clickHandler(event:MouseEvent):void
			{
				expand = !expand;
			}
			
			
			/**
			 * 
			 * 是否展开内容
			 * 
			 */
			
			public function get expand():Boolean
			{
				return mp::expand as Boolean;
			}
			
			/**
			 * @private
			 */
			public function set expand($value:Boolean):void
			{
				if (expand!= $value)
				{
					mp::expand = $value;
					
					applyCallback(callbackExpand);
				}
			}
			
			/**
			 * @private
			 */
			private function callbackExpand():Boolean
			{
				var result:Boolean = (currentState == "image") || 
					(expandBtn && textContainer && text);
				if (result)
				{
					if (currentState == "media")
					{
						expandBtn.selected = expand;
						textContainer.height = expand ? Math.max(60, text.height) : 60;
					}
				}
				return result;
			}
			
			
			/**
			 * 
			 * 数据源。
			 * 
			 */
			
			[Bindable]
			public function get data():Object
			{
				return mp::data;
			}
			
			/**
			 * @private
			 */
			public function set data($value:Object):void
			{
				mp::data = $value;
				
				applyCallback(callbackData);
			}
			
			/**
			 * @private
			 */
			private function callbackData():Boolean
			{
				var result:Boolean = text && textContainer && 
					contentContainer && subTitleLabel;
				if (result)
				{
					textContainer.visible = data && data.content;
					
					if (data)
					{
						currentState = ObjectUtil.convert(data.isShowTitle, Boolean) ? "media" : "image";
						if (currentState == "media")
						{
							if(!StringUtil.isEmpty(data.subTitle))
							{
								if(!contentContainer.containsElement(subTitleLabel))
									contentContainer.addElementAt(subTitleLabel, 1);
							}
							else
							{
								if (contentContainer.containsElement(subTitleLabel))
									contentContainer.removeElement(subTitleLabel);
							}
						}
					}
					resolveMedia();
					
					expand = false;
				}
				return result;
			}
			
			
			/**
			 * @private
			 */
			[Bindable]
			private var title:String;
			
			/**
			 * @private
			 */
			private var video:VideoPlayer;
			
			private var image:Image;
			
			/**
			 * @private
			 */
			mp var data:Object;
			
			/**
			 * @private
			 */
			mp var expand:Boolean;
			
		]]>
	</fx:Script>
	
</wt:Vessel>
